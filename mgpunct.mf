% THIS IS THE OFFICIAL COMPUTER MODERN SOURCE FILE punct.mf BY D E KNUTH.
% IT MUST NOT BE MODIFIED IN ANY WAY UNLESS THE FILE NAME IS CHANGED!

% This file contains punctuation marks common to roman and italic styles.

% Codes \0041, \0043, \0045, \0047--\0054,\ %
%% \0056, \0057, \0072, \0073, \0075, \0100,\par
% \0133, \0135, and \0140 are generated, as well as code |spanish_shriek|
% (for a Spanish exclamation point) if that code value is known.

cmchar "Exclamation point";
beginchar("!",5u#+width_adj#,asc_height#,0);
italcorr asc_height#*slant-2u#-.5width_adj#+.5dot_size#;
adjust_fit(0,0);
pos3(dot_size,0); pos4(dot_size,90);
x3l=hround(.5w-.5dot_size); y4l=-dot_o; z3=z4; dot(3,4);  % dot
numeric bot_width;
bot_width=if hefty:max(hround .8dot_size,fine.breadth)
 else: .5[hair,thick_hair] fi;
pos1(w,0); pos2(bot_width,0); pos5(bot_width,0);
x1=x2=x3=x5; y2=.25[top y4r,x_height]+1; y5=.2[y2,y1];
if square_dots: top y1=h;
 filldraw stroke z1e--z2e;  % stem
else: top z0=(x1,h+.5oo); y1+.5dot_size=h+.5oo;
 fill pen_lrend2(down,fine,up)--z5r{up}..{up}z1r..{left}z0..{down}z1l
  ..{down}z5l--cycle;
fi
penlabels(0,1,2,3,4,5); endchar;

iff known spanish_shriek: cmchar "Spanish open exclamation point";
beginchar(spanish_shriek,5u#+width_adj#,asc_height#-desc_depth#,desc_depth#);
italcorr h#*slant-2u#-.5width_adj#+.5dot_size#;
adjust_fit(0,0);
pickup tiny.nib; pos3(dot_size,0); pos4(dot_size,90);
lft x3l=hround(.5w-.5dot_size); top y4r=h; z3=z4; dot(3,4);  % dot
numeric top_width;
top_width=if hefty:max(hround .8dot_size,fine.breadth) else: hair fi;
pickup fine.nib; pos1(dot_size,0); pos2(top_width,0);
x1=x2=x3; top y2=.25[bot y4l,h-x_height]-1;
if square_dots: bot y1=-d;
 filldraw stroke z1e--z2e;  % stem
else: bot z0=(x1,-d-o); y1-.5dot_size=-d-o;
 filldraw z1l...z0...z1r---z2r--z2l---cycle; fi  % stem and bulb
penlabels(0,1,2,3,4); endchar;

vardef hash_intc(expr a)(suffix $)(expr b)(suffix $$) =
 pair p_;
 p_=whatever[z[a]$,z[a+1]$]=whatever[z[b]$$,z[b+1]$$];
 p_
enddef;
cmchar "Hash mark (number sign)";
if monospace: compute_spread(.6x_height#,.7x_height#);
else: compute_spread(.45x_height#,.55x_height#); fi
beginchar("#",15u#,fig_height#,asc_depth#);
italcorr (math_axis#+.5(spread#+rule_thickness#))*slant-.5u#;
adjust_fit(0,0);
disable_transforms_around(.5(w,h));
numeric alpha.body,alpha.ends; alpha.body=82; alpha.ends=88;
pos1(rule_thickness,alpha.ends); pos2(rule_thickness,alpha.ends);
pos3(rule_thickness,alpha.ends); pos4(rule_thickness,alpha.ends);
pos5(rule_thickness,0); pos6(rule_thickness,0);
pos7(rule_thickness,0); pos8(rule_thickness,0);
y5=y7=h; y6=y8=0;
x3l=0; x2r=w;
z1=z3+whatever*dir alpha.body; z2=z4+whatever*dir alpha.body;
y1=y2; y3=y4; .5[y1,y3]=.5[y5,y6]; y1-y3=.9spread;
z5=z6+whatever*dir alpha.body; z7=z8+whatever*dir alpha.body;
.5[x7,x6]=.52[x3,x2];
x8-x6=.88spread;
fill z8l--z8r--hash_intc(3,l,7,r)--z4l--z4r--hash_intc(3,r,7,r)
 --hash_intc(1,l,7,r)--z2l--z2r--hash_intc(1,r,7,r)--z7r--z7l
 --hash_intc(1,r,7,l)--hash_intc(1,r,5,r)--z5r--z5l
 --hash_intc(1,r,5,l)--z1r--z1l--hash_intc(1,l,5,l)
 --hash_intc(3,r,5,l)--z3r--z3l--hash_intc(3,l,5,l)--z6l--z6r
 --hash_intc(3,l,5,r)--hash_intc(3,l,7,l)--cycle;
unfill hash_intc(1,l,5,r)--hash_intc(1,l,7,l)
 --hash_intc(3,r,7,l)--hash_intc(3,r,5,r)--cycle;
penlabels(1,2,3,4,5,6,7,8,15,27,36,48); endchar;

cmchar "Per cent sign";
beginchar("%",9u#+max(6u#,2fudge*(hair#+stem#)),
  body_height#,body_height#-asc_height#);
italcorr if hefty: .4asc_height#*slant-.5u# else: h#*slant-u# fi;
adjust_fit(0,0);
disable_transforms_around(.5(w,h));
y9=h-o; y10=-oo;
if hefty: x9=good.x(x5-eps); x10=good.x(x1+eps);
 draw z9--z10;  % diagonal
else:
pos9(.8rule_thickness,0); pos10(.8rule_thickness,0);
x9r=hround(w-2.4u); x10l=hround 2.5u;
fi
numeric left_curve,right_curve;
left_curve=stem;
pair ital_dir; ital_dir=(z9-z10) xscaled .5;
right_curve=max(fine.breadth,hround(fudged.hair if hefty:-2stem_corr fi));
pos1(vair,angle ital_dir); pos2(left_curve,180);
pos3(vair,180+angle ital_dir); pos4(right_curve,360);
y1r=h-thick_vair; x2r=hround .25u; x4r=hround(.5w-.7u);
y3r=floor(if monospace: .7 else: .42 fi\\ asc_height);
.5[x1,x3]=.5[x2,x4]; y2=y4=.5[y1,y3]; z1=z3+whatever*ital_dir;
make_paths1(rev.l) angled_super_arc.e(1,2,ital_dir)
 & angled_super_arc.e(2,3,ital_dir);  % left half of upper bowl
make_paths2(rev.l) angled_super_arc.e(3,4,ital_dir)
 & angled_super_arc.e(4,1,ital_dir); % right half of upper bowl
pos5(vair,angle ital_dir); pos6(left_curve,180);
pos7(vair,180+angle ital_dir); pos8(right_curve,360);
z5-z6=z1-z2; z5-z7=z1-z3; z5-z8=z1-z4;
y7r=-d; x8r=w-hround .15u;
make_paths3(rev.l) angled_super_arc.e(5,6,ital_dir)
 & angled_super_arc.e(6,7,ital_dir)
 & angled_super_arc.e(7,8,ital_dir) & angled_super_arc.e(8,5,ital_dir);
pos9'(hair,180); z9'r=z9l;
pos11(hair,angle ital_dir); x11=.4[x1,x9]; y11r=y1r-2.5hair;
make_paths4(rev.r) z1e..{right}z11e;
fill rpath3 & cycle;
unfill lpath3 & cycle;
fill pen_pointend(10r,9r,fine,9l,10l){z10l-z9l}..combine(rpath4,rpath2)
 & rpath1 & combine(rpath2,lpath4)
 & combine(z11l{right}..{z9r-z10r}z9r, z9l--pen_pointend(9l,10l,fine,10r,9r))
 --cycle;
unfill combine(lpath4,subpath(.1,infinity) of lpath2) & lpath1 & cycle;
penlabels(1,2,3,4,5,6,7,8,9,10,11,12); endchar;

cmchar "Apostrophe";
beginchar("'",5u#,asc_height#,0);
italcorr asc_height#*slant+.5dot_size#-2u#;
adjust_fit(0,0);
x1+.5dot_size=w; y1+.5dot_size=h;
disable_transforms_around((0,h-.5comma_depth));
if monospace: comma(1,a,dot_size,.28u,vround 1.5comma_depth,1);  % large comma
else: comma(1,a,dot_size,0,.88comma_depth); fi  % comma with increased jut
penlabels(1); endchar;

cmchar "Left parenthesis";
beginchar("(",7u# if monospace: -u# fi,body_height#,paren_depth#);
italcorr body_height#*slant-.5u#;
adjust_fit(0,0);
pos1(vair,-90); pos2(stem,0); pos3(vair,90);
x1r=x3r=w; x2l=0;
y1l=h; y2=.5[y1,y3]; y3l=-.7desc_depth;
fill stroke z1e{3(x2e-x1e),y2-y1}...z2e
 ...{3(x3e-x2e),y3-y2}z3e;  % arc
penlabels(1,2,3); endchar;

cmchar "Right parenthesis";
beginchar(")",7u# if monospace: -u# fi,body_height#,paren_depth#);
italcorr math_axis#*slant-.5u#;
adjust_fit(0,0);
pos1(vair,90); pos2(stem,0); pos3(vair,-90);
x1l=x3l=0; x2r=w;
y1r=h; y2=.5[y1,y3]; y3r=-.7desc_depth;
fill stroke z1e{3(x2e-x1e),y2-y1}...z2e
 ...{3(x3e-x2e),y3-y2}z3e;  % arc
penlabels(1,2,3); endchar;

cmchar "Asterisk";
beginchar("*",9u#,
  if low_asterisk:math_axis#+.5x_height# else: body_height# fi,0);
italcorr h#*slant-.75u#;
adjust_fit(0,0);
numeric ast_flare; ast_flare=hround .9[thin_join,stem];
numeric mid_flare; mid_flare=hround .6[thin_join,stem];
x0=.5w; y0=h-(.57-.3slant)*w;
disable_transforms_around(z0);
for d=30 step 60 until 330:
 z[d]=z0+dir d scaled (.57-.3slant)*w;
 z[d]mid=.4[z0,z[d]];
 numeric theta; theta=angle(z[d]-z0);
 fill
  z0+.5(0,-.5thin_join)rotated theta
  ---z[d]mid+.5(0,-.5mid_flare)rotated theta
  ...z[d]+.5(-ast_flare,-ast_flare)rotated theta
  ..z[d]..z[d]+.5(-ast_flare,ast_flare)rotated theta
  ...z[d]mid+.5(0,.5mid_flare)rotated theta
  ---z0+.5(0,.5thin_join)rotated theta--cycle; endfor  % diagonal at angle |d|
labels(0,30,90,150,210,270,330); endchar;

cmchar "Plus sign";
beginarithchar("+");
pos1(rule_thickness,0); pos2(rule_thickness,0);
pos3(rule_thickness,90); pos4(rule_thickness,90);
x1=x2=good.x .5w; .5[y1,y2]=math_axis;
disable_transforms_around(.5[z1,z2]);
x3=0; x4=w; y3=y4=math_axis;
y1-y2=x4-x3;
fill z1r--z1l--hash_intc(1,l,3,r)--z3r--z3l--hash_intc(1,l,3,l)
 --z2l--z2r--hash_intc(1,r,3,l)--z4l--z4r--hash_intc(1,r,3,r)--cycle;
penlabels(1,2,3,4); endchar;

cmchar "Comma";
numeric dot_diam#; dot_diam#=if monospace: 5/4 fi\\ dot_size#;
define_whole_blacker_pixels(dot_diam);
beginchar(",",5u#,dot_diam#,comma_depth#);
adjust_fit(0,0);
x1-.5dot_diam=hround(w-dot_diam); y1-.5dot_diam=-dot_o;
disable_transforms_around((.5w,0));
comma(1,a,dot_diam,0,comma_depth-dot_o);  % dot and tail
penlabels(1); endchar;

cmchar "Period";
numeric dot_diam#; dot_diam#=if monospace: 5/4 fi\\ dot_size#;
define_whole_blacker_pixels(dot_diam);
beginchar(".",5u#,dot_diam#,0);
adjust_fit(0,0); pickup fine.nib;
pos1(dot_diam,0); pos2(dot_diam,90);
x1l=hround(.5w-.5dot_diam); y2l=-dot_o; z1=z2; dot(1,2);  % dot
penlabels(1,2); endchar;

cmchar "Virgule (slash)";
beginchar("/",9u#,body_height#,desc_depth#);
italcorr body_height#*slant-.5u#;
adjust_fit(0,0);
pos1(rule_thickness,0); pos2(rule_thickness,0);
x1r=w; top y1=h;
x2l=0; bot y2=-o;
fill stroke z1e--z2e;  % diagonal
penlabels(1,2); endchar;

cmchar "Colon";
numeric dot_diam#; dot_diam#=if monospace: 5/4 fi\\ dot_size#;
define_whole_blacker_pixels(dot_diam);
beginchar(":",5u#,x_height#,0);
italcorr x_height#*slant+.5dot_diam#-2u#;
adjust_fit(0,0);
pos1(dot_diam,0); pos2(dot_diam,90);
x1l=hround(.5w-.5dot_diam); y2r=h+oo; z1=z2; dot(1,2);  % upper dot
pos3(dot_diam,0); pos4(dot_diam,90);
x3=x1; y4l=-dot_o; z3=z4; dot(3,4);  % lower dot
penlabels(1,2,3,4); endchar;

cmchar "Semicolon";
numeric dot_diam#; dot_diam#=if monospace: 5/4 fi\\ dot_size#;
define_whole_blacker_pixels(dot_diam);
beginchar(";",5u#,x_height#,comma_depth#);
italcorr x_height#*slant+.5dot_diam#-2u#;
adjust_fit(0,0);
pos1(dot_diam,0); pos2(dot_diam,90);
x1r=w; y2r=h+oo; z1=z2; dot(1,2);  % upper dot
x3=x1; y3-.5dot_diam=-dot_o;
begingroup
 disable_transforms_around((.5w,0));
 comma(3,a,dot_diam,0,comma_depth-dot_o)  % lower dot and tail
endgroup;
penlabels(1,2,3); endchar;

cmchar "Equals sign";
compute_spread(.45x_height#,.55x_height#);
beginchar("=",14u#,v_center(spread#+rule_thickness#));
italcorr h#*slant-.5u#;
adjust_fit(0,0);
forsuffixes $=1,2,3,4: pos$(rule_thickness,90); endfor
x1=0; x3=x1; x2=x4=w;
y1=y2; y3=y4; y1-y3=.9spread; .5[y1,y3]=math_axis;
disable_transforms_around((.5w,math_axis));
fill stroke z1e--z2e;
fill stroke z3e--z4e;
labels(1,2,3,4); endchar;

cmchar "At sign";
beginchar("@",14u#,asc_height#,0);
italcorr .7asc_height#*slant-.5u#;
numeric ital_slant; ital_slant=.286745;
disable_transforms_around((.5w,.5h));
adjust_fit(0,if hefty or monospace:-2u# else: 0 fi);
pos0(fudged.hair,0); pos1(thick_vair,angle (ital_slant,1));
pos2(fudged.stem,180); pos3(thick_vair,180+angle (ital_slant,1));
pos4(fudged.stem,-180); pos5(fudged.stem,-180);
pos6(vair,-90); pos7(thick_hair,0);
pos8(.8vair,90); pos9(curve,180);
pos10(vair,270); pos11(vair,-60);
.5[x1,x3]=.55[x2,x0]; y0=y2=.5[y1,y3];
x1-x3=ital_slant*(y1-y3);
x1l:=.55[x2l,x0l]+ital_slant*(y1l-y2l);
x3l:=.55[x2l,x0l]+ital_slant*(y3l-y2l);
x8=.5[x9l,x7l]; x10=.5w; y7=.5[y6l,y8l]; y9=.5[y8,y10];
x9r=0; x7r=w; y8r=h; y10r=-o;
y4=.76[y10l,y8l];
y6=.23[y10l,y8l];
y1r=y4+oo; y3r=y6r;
x6l=1/3[x5l,x7l]; y11=good.y(.1[y10,y8]-.5);
x4r-x0l=ital_slant*(y4r-y0l);
if hefty or monospace: lft x2r=hround(5u-.5fudged.stem);
 rt x0r=hround(w-3u); x11r=x0r;
else: x2r=hround 4u; x0=w-4.6u; x11r=x6r; fi
y5=2/3[y7,y6]; z5r=whatever[z4r,z0l];
make_paths1(rev.l) angled_super_arc.e(0,1,(ital_slant,1))
 & angled_super_arc.e(1,2,(ital_slant,1));
make_paths2(rev.l) angled_super_arc.e(2,3,(ital_slant,1))
 & angled_super_arc.e(3,0,(ital_slant,1));
make_paths3(rev.l) super_arc.e(8,9) & super_arc.e(9,10)
 & term.e(10,11,right,1,4);  % left outer bowl and terminal
if hefty or monospace: {{interim superness:=hein_super;
  filldraw stroke super_arc.e(0,8)}};  % link
else: (x,y6r)=whatever[z6l,z7l]; x6r:=x;
 make_paths4(rev.l) z5e...z6e{right}...{up}z7e
  & super_arc.e(7,8); fi % stem and link
fill combine(pen_pointend(5l,4l,fine,4r,5r)--z5r,rpath1)
 & combine(rpath2,z4r--rpath4) & rpath3--lpath3 & lpath4--cycle;
 unfill lpath1 & lpath2 & cycle;
penlabels(0,1,2,3,4,5,6,7,8,9,10,11); endchar;

cmchar "Left bracket";
numeric wd#; wd#=max(5u#,4.5u#+.5if hefty:stem# else:rule_thickness# fi);
beginchar("[",wd#,body_height#,paren_depth#);
italcorr body_height#*slant;
adjust_fit(0,0);
numeric top_thickness,side_thickness;
if hefty: top_thickness=vair;
 side_thickness=max(crisp.breadth,stem-2stem_corr);
else: top_thickness=vair; side_thickness=stem; fi;
pos1(side_thickness,0); pos2(side_thickness,0);
y1=h-.5top_thickness; y2=-.7desc_depth+.5top_thickness; x1l=x2l=0;
pos3(top_thickness,90); pos4(top_thickness,90);
pos5(top_thickness,90); pos6(top_thickness,90);
x3=x5=x1l; x4=x6=w; y3r=y4r=y1; y5l=y6l=y2;
fill pen_lrend4(right,crisp,left)--pen_pointend(4r,1l,crisp,2l,6l)
 --pen_lrend6(right,crisp,left)--(x2r,y6r)--(x1r,y4l)--cycle;
penlabels(1,2,3,4,5,6); endchar;

cmchar "Right bracket";
numeric wd#; wd#=max(5u#,4.5u#+.5if hefty:stem# else:rule_thickness# fi);
beginchar("]",wd#,body_height#,paren_depth#);
italcorr body_height#*slant-2u#+.5if hefty:stem# else:rule_thickness# fi;
adjust_fit(0,0);
numeric top_thickness,side_thickness;
if hefty: top_thickness=vair;
 side_thickness=max(crisp.breadth,stem-2stem_corr);
else: top_thickness=vair; side_thickness=stem; fi;
pos1(side_thickness,0); pos2(side_thickness,0);
y1=h-.5top_thickness; y2=-.7desc_depth+.5top_thickness; x1r=x2r=w;
pos3(top_thickness,90); pos4(top_thickness,90);
pos5(top_thickness,90); pos6(top_thickness,90);
x3=x5=x1r; x4=x6=0; y3r=y4r=y1; y5l=y6l=y2;
fill pen_rlend6(left,crisp,right)--pen_pointend(6r,2r,crisp,1r,4l)
 --pen_rlend4(left,crisp,right)--(x1l,y4l)--(x2l,y6r)--cycle;
penlabels(1,2,3,4,5,6); endchar;

cmchar "Reverse apostrophe";
beginchar("`",5u#,asc_height#,0);
italcorr asc_height#*slant+.5dot_size#-2u#;
adjust_fit(0,0);
disable_transforms_around((0,h-comma_depth));
x1=.5dot_size; y1+.5dot_size=h-comma_depth+o;
if monospace: ammoc(1,a,dot_size,.28u,vround 1.5comma_depth);  % large ammoc
else: ammoc(1,a,dot_size,0,.88comma_depth); fi  % normal ammoc
penlabels(1); endchar;
