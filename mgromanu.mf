% THIS IS THE OFFICIAL COMPUTER MODERN SOURCE FILE romanu.mf BY D E KNUTH.
% IT MUST NOT BE MODIFIED IN ANY WAY UNLESS THE FILE NAME IS CHANGED!

% Computer Modern Roman upper case:
% These letters were originally coded by D. E. Knuth in November, 1979,
% inspired by the Monotype faces used in {\sl The Art of Computer Programming}.
% Sans serif designs by Richard Southall were added in April, 1982.
% The programs were revised for the new \MF\ conventions in 1985.

% Character codes \0101 through \0132 are generated.

cmchar "The letter A";
beginchar("A",12u#,cap_height#,0);
adjust_fit(0,0);
nibthinstems(1,2,5,10); nibhairs(3,4,7,8,11,12);
x1r=x3l; x2r=w-u; x4r=2u;
x2r-x1r=x3r-x4r;
y1r=y3r=h+o; y2l=y4l=-o;
x6=x1l-2u;
angled_hook_in_path1(6,1,2,b,45);
z7l=z2l; nib_align(z7l,z8l); x8l=x2r+.75u;
x9=x4r-2u; x10=x4l+2u; y10l=y4l;
swoop_in_path9(9,10,a);
z11l=z10l; nib_align(z12l,z11l); x12l-x10r=x8l-x2r;
pos13(cap_bar,80); pos14(cap_band,80);
y13=y14=.5[ypart lineintersection(1l,2l,3l,4l), y4l];
z13l=whatever[z3r,z4r]; z14l=whatever[z1r,z2r];
z15r=whatever[z3r,z4r]; z15l=whatever[z3r,z4r]; z15=.5[z15r,z15l];
y15r-y15l=stem; y15=.5[y4r,y3r];
p15=side_hook_path(15);
pair a_point_dir; a_point_dir=(z2r-z1r) xscaled -1;
fill z2l--z8l--z8r--lineintersection(8r,7r,2r,1r)
  --rpath1
  & (lpath1--z2l combined (z3r--z15r))
  & p15 & (z15l--z4r var_combined rpath9)
  & lpath9--z12l
  --(z12r--z11r var_combined rpath9
      combined (z4l--lineintersection(4l,3l,13l,14l)))
  --lineintersection(13l,14l,1l,2l)
  --cycle;
unfill lineintersection(1l,2l,3l,4l)--lineintersection(1l,2l,13r,14r)
  --lineintersection(3l,4l,13r,14r)--cycle;
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15); endchar;

cmchar "The letter B";
beginchar("B",12.5u#,cap_height#,0);
italcorr .75cap_height#*slant-.5u#;
adjust_fit(cap_serif_fit#,0);
numeric left_stem,right_curve,middle_weight;
left_stem=cap_stem-hround 2stem_corr; middle_weight=.6vair+.5;
pos1(left_stem,0); pos2(left_stem,0);
x1l=x2l=cap_jut; y1=h; y2=0;
penpos3(thin_cap_band,90); penpos4(thin_cap_band,90);
penpos6(middle_weight,-90); penpos7(middle_weight,-90);
penpos8(middle_weight,90); penpos9(middle_weight,90);
penpos5(right_curve-stem_corr-.5stem*slant,0); penpos10(right_curve,0);
penpos11(vair,-90); penpos12(vair,-90);
z3r=z1r; y4=y3; y5=(.47+.47*slant)[y4,y6]; y6=y7; y7l-y8l=vair;
z12r=z2r; y11=y12; y10=(.5-.5*slant)[y11,y9]; y8=y9; .5[y7l,y8l]=.52h;
x6=.45[x1r,x5l]; x9=.4[x1r,x5l];
x11=x4+.5u; x7=x8=x1r; x9l:=x9+.25u;
x5r=x10r-hround(.3u)-1.5u*slant; x10r=w;
if serifs:
 right_curve=cap_curve-stem_corr; x4=.4[x1,x5r];
else:
 right_curve=cap_curve-3stem_corr; x4=.5[x1,x5r-u];
 x4l:=x4l-.5u; x9l:=x9l-.5u;
fi
x6l:=x6l-.5u; x11l:=x11l-.5u;
make_paths1(rev.r) pulled_arc.e(4,5) & pulled_arc.e(5,6)..z7e;  % upper lobe
make_paths2(rev.r) z8e..pulled_arc.e(9,10) & pulled_arc.e(10,11);  % lower lobe
if serifs:
 p1=serif_path(1,2,a,1/3,cap_jut,left);  % upper serif
 p2=serif_path(2,1,c,1/3,slant_adjusted_jut(cap_jut),left); % lower serif
 p3=internal_serif_path(1,2,3l,b,1/3,jut);
 p4=internal_serif_path(2,1,12l,d,1/3,jut);
else:
 p1=pen_angle(left,z1l,tiny,down);
 p2=pen_angle(down,z2l,tiny,right);
 p3=z3l; p4=z8l;
fi
fill p1--p2--combine(rpath2,rpath1)--cycle;
unfill z7l--p3--lpath1--cycle;
unfill z8l--lpath2--p4--cycle;
math_fit(0,.5ic#);
penlabels(1,2,3,4,5,6,7,8,9,10,11,12);
endchar;

vardef C_barb =
 path p_[];
 pair c_barb_dir; c_barb_dir = (z2-z1) yscaled 3;
 p_1=z2l{right}...{-c_barb_dir}pen_lrend1(-c_barb_dir,fine,z6r-z1r);
 p_2=z1r{up}...{left}z2r;
 p_6=pen_rlend6(z6r-z1r,fine,z1'-z6l)--z1';
 p_1--var_combine(p_6,p_2)
enddef;

cmchar "The letter C";
beginchar("C",12u#,cap_height#,0);
italcorr cap_height#*slant-.5u#;
adjust_fit(0,0);
y1l=h-hair;
x2l=u-o;
y3l=-o;
%x4l=w-u+o;
crescent_path1(1,2,3,4,nib_dir);
nibstems(9,10);
x9=x10; x9l-x2r=2u;
p9=(x9l-u,h)--(x9l-u,y2r);
y9l=ypart (p9 intersectionpoint rpath1);
p10=(x9l-.3u,y2r)--(x9l-.3u,0);
nib_align(z10l,p10 intersectionpoint rpath1);
nibhairs(7',9',10',11);
z7'r=z7r; z9'r=z9l;
z10'l=z10r; x11r=0; nib_align(z11l,z10r);
pos5(nib_hair,0); pos6(nib_hair,0); x5=x6; x5l=x9r+u; y6=0; y5r=h+o;
nibstems(7); y7r=h+o; nib_align(z7,z9);
x8=x4l;
swoop_out_path8(7,8);
fill var_combine(lineintersection(9l,10l,10'r,11r)--z11r,rpath1) & lpath1 &
 var_combine(combine(rpath1,z6r--z5r),lpath8 & rpath8)--z9l--cycle;
unfill lineintersection(10r,9r,9'l,7'l)--z7'l var_combined lpath8 & rpath8
  combined (z5l--z6l) var_combined rpath1 combined (z11l--z10r)--cycle;
penlabels(1,2,3,4,5,6,7,8,9,10,11); endchar;

cmchar "The letter D";
beginchar("D",11u#,cap_height#,0);
italcorr .7cap_height#*slant-.5u#;
adjust_fit(0,0);
numeric d_rad; d_rad=2u;
nibstems(1,2,3,4);
x1l=u; x2r=x3r=w-u;
y1r=y2r=h; y4l=-o; overlap(4,3);
x5=x1l-.5u;
swoop_in_path5(5,4,a);
z0=(.5u,h+o);
nibstems(6,7);
x6l=x7l=2u;
y6r=y1l-.75u; y7l=y4r+.5u;
p6=small_point_path(6,7,b,.5u);
p7=small_point_path(7,6,c,.5u);
pos8(nib_hair,0); pos9(nib_hair,0);
x8l=x9l=x6r+u;
y8=h+o; y9=-o;
pos10(stem,90); z10l=.5[z7l,z6l];
p10=side_hook_path(10);
fill z3r--pen_point(3r,2r,2d_rad,1r)--z1r{left}..z0
 & z0--pen_point(0,1l,stem,2l)
 --((lineintersection(1l,2l,8l,9l)--z9l) var_combined rpath5)
 & lpath5--z4l--cycle;
fill p6--p10--p7--cycle;
unfill pen_point(1l,2l,2d_rad-stem,3l)--z3l var_combined rpath5
 combined (z9r--lineintersection(9r,8r,1l,2l))--cycle;
penlabels(0,1,2,3,4,5,6,7,8,9,10); endchar;

def E_center(expr beak_inangle_factor) =
pos5(.8cap_bar,-90); pos6(.8hair,0);
y5l=vround(.515[y2,y1]+.5cap_bar); x5=x1;
pos0(cap_bar,90); pos7(.8hair,0);
z0=z5; x6=x7; 1.15(y6-y5l)=y0l-y7;
begingroup
  in_beak_inangle:=beak_inangle_factor * beak_inangle;
  save beak_inangle; beak_inangle=in_beak_inangle;
  p5=two_arm_path(5,6,f,0,7,g,E_mid_darkness);
endgroup;
enddef;

cmchar "The letter E";
beginchar("E",12u#,cap_height#,0);
italcorr cap_height#*slant-beak_jut#-.5u#;
adjust_fit(0,0);
nibstems(1,2,3,5,7,8);
x5r=w-u;
y5l=-o;
x6=.5u;
swoop_in_path6(6,5,a);
tick_path5(5,f,up,.75u);
x4=w-.25u;
y3r=h+o;
x1=.5w;
overlap(1,3);
swoop_out_path3(3,4);
x1=x2;
y2l=-o;
x7=x8; x1l-x7r=u;
nib_align(z7,z1);
y8l=y2r+3hair;
p7=small_point_path(7,8,b)(.5u);
p8=small_point_path(8,7,c)(.5u);
nibstems(9); z9=(x1r,.57[y5r,y3l]); x10=x4-u;
swoop_out_path9(9,10);
z13=(.5u,h+o-(stem*ypart nib_dir)-2hair);
z14=(.5[x1,x1r],h+o);
tilde_path13(13,14,d,u);
p2=((z2r--z1r) var_combined lpath9) & ((rpath9--z9l) combined (z2r--z1r));
fill p7--p8--cycle;
fill lpath13 & rpath13 & cycle;
fill lpath5 var_combined rpath6 combined p2 var_combined lpath3 & rpath3
  --((z1l--z2l) var_combined rpath6) & lpath6 & rpath5
  --cycle;
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14); endchar;

cmchar "The letter F";
beginchar("F",12u#,cap_height#,0);
italcorr cap_height#*slant-beak_jut#-.25u#;
adjust_fit(0,0);
nibstems(1,2,3,5,7,8);
x4=w+u;
y3r=h+o;
x1=.6w;
overlap(1,3);
swoop_out_path3(3,4);
x1=x2;
y5l=-o;
overlap(5,2);
x6=.5u;
swoop_in_path5(6,5,a);
x7=x8; x1l-x7r=u;
nib_align(z7,z1);
y8l=y2r+2hair;
p7=small_point_path(7,8,b)(.5u);
p8=small_point_path(8,7,c)(.5u);
pos9(stem,90); pos10(stem,90);
y9r=y10r=x_height;
x9=x1r; x10=w-u-hair;
pos11(nib_hair,0); pos12(nib_hair,0);
x11l=x12l=x10; y11=y4; y12=y2r;
z13=(1.5u,h+o-(stem*ypart nib_dir)-2hair);
z14=(.5[x1,x1r],h+o);
tilde_path13(13,14,d,u);
fill p7--p8--cycle;
fill lpath13 & rpath13 & cycle;;
fill (z12r--z11r) var_combined lpath3 & rpath3--(z1l--z2l var_combined rpath5)
  & lpath5--z2r--z9l--z10l--z12l--cycle;
unfill (z9r--z1r) var_combined lpath3 combined (z11l--z10r)--cycle;
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14); endchar;

cmchar "The letter G";
if serifs: beginchar("G",14u#,cap_height#,0);
 numeric G_outer_jut;
 G_outer_jut=.8cap_jut;
 italcorr cap_height#*slant-1.5u#-.5cap_serif_fit#;
 adjust_fit(0,.5cap_serif_fit#);
 pos0(cap_stem,0); pos7(cap_stem,0);
 pos9(hair,0);
 x0r=w-G_outer_jut; y0=good.y(.3[bar_height,x_height]+1); x7r=x0r;
 if hefty: y7=0; pos5(cap_stem,0); x5=x0; y5=y0;
 else: pos5(cap_hair,0); pos8(cap_stem,0);
  y7=0; z5l=z8l; x8r=x7r; y8=.5[y7,y0]; x9r=x8r; y9=y4r; fi
 pos1(.7cap_hair,0); pos2(thin_cap_band,90);
 pos3(cap_curve,180); pos4(thin_cap_band,270);
 x1r=hround(w-1.5u-2u*slant); x3r=0;
 x2=.5[x3,x1]; x4=(.5+.5slant)[x3,x1];
 y2r=h+o; y4r=-o; y3=(.5-.2slant)[y2,y4];
 y1=min(vround max(.6h,x_height-.5vair),y2l-eps);
 %(x2l',y2l)=whatever[z2r,z1l]; x2l:=min(.5[x2l,x2l'],x2l+.5u);
 %(x4l',y4l)=whatever[z4r,z5l]; x4l:=min(x4l',x4l+.5u);
 pos6(.5[fine.breadth,cap_hair],0); x6r=x1r-beak_jut; y6=h+o;
 x1r-x1'=2cap_curve-fine; y1'=y1;
 path upper_arc; upper_arc=z1{x2-x1,10(y2-y1)}..z2{left};
 numeric t; t=xpart(upper_arc intersectiontimes (z6l--z1'));
 make_paths1(rev.l) pulled_arc.e(2,3);
 make_paths2(rev.l) pulled_arc.e(3,4)...z5e{up}; % arc
 p1=dish_serif_path(0,7,a,1/3,cap_jut,b,1/3,G_outer_jut);  % serif
 p2=pen_rlend6(up,fine,z1'-z6l)--z1';
 pair G_spur_dir; G_spur_dir=down;
 p3=z4r{right}
  .. tension .75 and .75 ..{G_spur_dir}pen_lrend9(G_spur_dir,crisp,z0r-z9r);
 pos1''(1r-1l,0); z1''=z1 + down scaled (fine + eps);
 fill C_barb & rpath1 & combine(rpath2, p3)--p1--lpath2 & lpath1 & cycle;
 math_fit(-.3cap_height#*slant-.5u#,ic#);
else: beginchar("G",12u#,cap_height#,0);
 italcorr cap_height#*slant-.5u#;
 adjust_fit(0,0);
 pickup fine.nib; pos1(1.2flare,80); pos2(slab,90);
 pos3(cap_curve,180); pos4(slab,270); pos5(flare,270);
 rt x1r=hround(w-1.35u); x2=x4=.5w+u;
 lft x3r=hround max(u,2u-.5cap_curve); rt x5l=hround(w-1.2u);
 top y1r=vround .93h+oo; top y2r=h+oo; y3=.5h;
 bot y4r=-oo; bot y5r=vround .07h-oo;
 filldraw stroke rterm.e(2,1,right,.9,4)&super_arc.e(2,3)
  & super_arc.e(3,4) & term.e(4,5,right,1,4); % arc and terminals
 pos0(stem,0); pos7(stem,0);
 z7r=z5r; x0=x7; top y0=1+vround .35[bar_height,x_height];
 filldraw stroke z0e--z7e;  % stem
 pos8(cap_bar,90); pos9(cap_bar,90);
 z0r=z9r; y8=y9; lft x8=hround x4;
 filldraw stroke z8e--z9e;  % bar
 math_fit(-.3cap_height#*slant-.5u#,.5ic#); fi
penlabels(0,1,1',2,3,4,5,6,7,8,9); endchar;

cmchar "The letter H";
beginchar("H",13u#+width_adj#,cap_height#,0);
italcorr cap_height#*slant-cap_serif_fit#+cap_jut#-2.5u#+min(.5cap_stem#,u#);
adjust_fit(cap_serif_fit#,cap_serif_fit#);
pos1(cap_stem,0); pos2(cap_stem,0);
pos3(cap_stem,0); pos4(cap_stem,0);
x1l=x2l=cap_jut; x3=x4=w-x1;
y1=y3=h; y2=y4=0;
penpos5(thick_cap_band,90); penpos6(thick_cap_band,90);
x5=x1r; x6=x3l; y5=y6=.51h;
make_paths1(rev.r) z5e--z6e;
if serifs:
 numeric inner_jut;
 if x1r+cap_jut+.5u+1<=x3l-cap_jut: inner_jut=cap_jut;
 else: x1r+inner_jut+.5u+1=x3l-inner_jut; fi
 p1=dish_serif_path(1,2,a,1/3,cap_jut,b,1/3,inner_jut);  % upper left serif
 p2=dish_serif_path(2,1,c,1/3,slant_adjusted_jut(cap_jut),
  d,1/3,inner_jut);  % lower left serif
 p3=dish_serif_path(3,4,e,1/3,inner_jut,
  f,1/3,slant_adjusted_jut(cap_jut));  % upper right serif
 p4=dish_serif_path(4,3,g,1/3,inner_jut,h,1/3,cap_jut);  % lower right serif
else:
 p1=pen_rlstem1(tiny,2);
 p2=pen_lrstem2(tiny,1);
 p3=pen_rlstem3(tiny,4);
 p4=pen_lrstem4(tiny,3);
fi
fill p1--p2--lpath1--p4--p3--rpath1--cycle;
math_fit(0,.5ic#); penlabels(1,2,3,4,5,6); endchar;

cmchar "The letter I";
beginchar("I",9u#,cap_height#,0);
italcorr cap_height#*slant-.25u#;
adjust_fit(0,0);
nibstems(1,2,4,5,6);
x1r=x2r=w-u; x3=.5u;
y1r=h+o; y4l=-o; overlap(4,2);
swoop_in_path3(3,4,a);
p1=small_point_path(1,2,b,.5u);
nib_align(z5,z1); x5r=x1l-u; x6=x5; y6l=y4r+1.5u;
p5=small_point_path(5,6,c,.5u);
p6=small_point_path(6,5,d,.5u);
z7=(.5u,h+o-(stem*ypart nib_dir)-2hair);
z8=(x1l,h+o);
tilde_path7(7,8,f,u);
fill z2r--(p1--z2l var_combined rpath3) & lpath3--cycle;
fill p5--p6--cycle;
fill lpath7 & rpath7 & cycle;
penlabels(1,2,3,4,5,6,7,8); endchar;

cmchar "The letter J";
beginchar("J",9u#,cap_height#,0);
numeric J_outer_jut;
J_outer_jut=slant_adjusted_jut(cap_jut)less;
italcorr cap_height#*slant-cap_serif_fit#
 +.75cap_jut#-2.5u#+min(.5cap_stem#,u#);
adjust_fit(0,cap_serif_fit#);
pos1(cap_stem',0); pos2(cap_stem',0);
y1=h; x1r=w-J_outer_jut; x2=x1; y2=.25h;
if serifs:
 pos4(cap_hair,-180);
 pos5(.8[stem,flare],-180);
 y3r=-.8o; x5r=0; z5r=z4r;
 set_bulb_points(vair,.6-.6slant,1.1, 2,3,4,5);
 make_paths1(rev.r) super_arc.e(2,3);  % stem and arc
 p1=dish_serif_path(1,2,a,1/3,1.2cap_jut,b,1/3,J_outer_jut);  % serif
 fill p1--lpath1 & bulb_path(3,4,5,left) & rpath1--cycle;
else: filldraw stroke z1e--z2e;  % stem
 pickup fine.nib; pos3(cap_stem',0); z3=z2;
 pos4(flare,angle(6.5u,-h)); pos5(1.1flare,-100);
 bot y4r=-o; x4r=.5[x5r,x3r]; lft x5r=hround.75u; bot y5r=vround.06h-o;
 filldraw stroke z3e{down}....term.e(4,5,left,1,4); % arc and terminal
fi
math_fit(0,.5ic#-.5u#); penlabels(1,2,3,4,5); endchar;

cmchar "The letter K";
beginchar("K",13.5u#,cap_height#,0);
italcorr cap_height#*slant-.5u#;
adjust_fit(cap_serif_fit#,cap_serif_fit#);
numeric right_jut,stem[],alpha[];
if serifs: right_jut=angled_jut(cap_jut,(w,h));
else: right_jut=hround(.4tiny); fi
pos1(fudged.cap_stem,0); pos2(fudged.cap_stem,0);
penpos1'(abs(z1r-z1l),0);
x1l=x2l=cap_jut; y1=h; y2=0;
z1'=.93[z2,z1];
stem2=max(tiny.breadth,fudged.cap_stem-3stem_corr);
stem1=max(tiny.breadth,.5[hair,thick_hair] if hefty:-3stem_corr fi);
y3=h; x3r=x6r-hround(.3u)-2u*slant;
y6=0; x6r=w-right_jut;
x4=x1; y4=.31h;
alpha1=diag_ratio(1,.5(stem1),y3-y4,x3r-x4);
penpos3(alpha1*(stem1),0); penpos4(whatever,-90);
alpha2=diag_ratio(1,.5(stem2),y1'-y6,x6r-x1);
penpos6(alpha2*(stem2),0);
forsuffixes $=l,r: y3'$=h; y6'$=0; z4$=z3'$+whatever*(z3-z4);
 z5$=z6'$+whatever*(z1'-z6)=whatever[z3,z4]; endfor
z5=.5[z5l,z5r];
z3'r=z3r+whatever*(z3-z4);
% we have also |z3'l=z3l+penoffset z4-z3 of currentpen+whatever*(z3-z4)|;\]
z6'r=z6r+whatever*(z1-z6);
z6'l=z6l+whatever*(z1-z6);
% Calculate intersection points of diagonals
z13=lineintersection(1r,2r,3l,4l);
z24=lineintersection(1r,2r,3r,4r);
z46=lineintersection(3r,4r,5l,6l);
z36=lineintersection(5r,6r,3r,4r);
if serifs:
 numeric inner_jut[];
 (inner_jut1,inner_jut2) = serif_inner_juts(.46,.23, 1,2,3,4);
 (inner_jut3,inner_jut4) = serif_inner_juts(.47,1/4, 2,1,6,5);
 p1=dish_serif_path(1,2,a,1/3,cap_jut,b,1/3,inner_jut1); % upper stem serif
 p2=dish_serif_path(2,1,c,1/3,slant_adjusted_jut(cap_jut),
  d,1/3,inner_jut3);  % lower stem serif
 p3=dish_serif_path(3,4,e,.45,inner_jut2,
  f,1/3,slant_adjusted_jut(.7right_jut))(dark); % upper diag
 p6=dish_serif_path(6,5,g,.28,inner_jut4,h,1/3,right_jut)(dark); % lower diag
else:
 p1=pen_rlstem1(tiny,2);
 p2=pen_lrstem2(tiny,1);
 p3=pen_rlstem3(tiny,4);
 p6=pen_lrstem6(tiny,5);
fi
fill p2--z24--z46--p6--z36--p3--z13--p1--cycle;
math_fit(0,.5ic#); penlabels(1,2,3,4,5,6); labels(13,24,46,36); endchar;

cmchar "The letter L";
beginchar("L",11u#-width_adj#,cap_height#,0);
adjust_fit(cap_serif_fit#,0);
pos1(cap_stem,0); pos2(cap_stem,0);
x1l=x2l=cap_jut; y1=h; y2=0;
pos3(slab,-90); pos4(hair,0);
y3r=0; x3=x2; y4=y3l+7/6beak+eps; x4r=w;
p4=arm_path(3,4,e,1.2beak_darkness,beak_jut_big);  % lower arm and beak
p1=dish_serif_path(1,2,a,1/3,cap_jut,b,1/3,1.15cap_jut);  % upper serif
p2=serif_path(2,1,c,1/3,slant_adjusted_jut(cap_jut),left);
p3=internal_serif_path(2,1,3l,d,1/3,cap_jut);
fill p2--p4--p3--p1--cycle;
math_fit(0,u#); penlabels(1,2,3,4); endchar;

cmchar "The letter M";
beginchar("M",16u#+width_adj#,cap_height#,0);
italcorr cap_height#*slant-cap_serif_fit#+cap_jut#-2.5u#+min(.5cap_stem#,u#);
adjust_fit(cap_serif_fit#,cap_serif_fit#);
numeric stem[]; % thicknesses of the four strokes
numeric M_jut; M_jut=slant_adjusted_jut(1.1cap_jut)less;
stem1=hround(thick_hair+stem_corr);
stem2=hround(fudged.cap_stem-4stem_corr);
stem3=hround(thick_hair-stem_corr);
stem4=hround(fudged.cap_stem-3stem_corr);
if stem4<stem1: stem4:=stem1; fi
pos1(stem1,0); pos2(stem1,0);
pos3(stem4,0); pos4(stem4,0);
x1=x2; x3=x4; x3r=w-cap_jut; x1l=M_jut;
y1=y3=h; y2=y4=0;
penpos5(stem2,0); penpos6(stem2,0); penpos7(stem3,0); penpos8(stem3,0);
x5l=x1; x7r-x6l=.7hair; x8=x3l; x6-x5=x8-x7; y5=y8=h; y6=y7;
if hefty: y6=if monospace: vround 1/3h else: o fi;
 numeric upper_notch,lower_notch;
 upper_notch=h-cap_notch_cut; lower_notch=y6+cap_notch_cut;
 x1'=x1r; z1'=whatever[z5l,z6l]; x3'=x3l; z3'=whatever[z7r,z8r];
 z0=whatever[z5r,z6r]=whatever[z7l,z8l];
 fill z5l..
  if y1'<upper_notch: {right}(x1'+1,upper_notch){down}... fi
  {z6-z5}diag_in(5l,6l,1,6r)..diag_out(7l,1,7r,8r){z8-z7}
  if y3'<upper_notch: ...{up}(x3'-1,upper_notch){right} fi
  ..z8r--diag_out(8r,1,8l,7l){z7-z8}
  if y0<=lower_notch: ..{z7-z8}z0{z5-z6}..
  else: ...{down}(x0+.5,lower_notch)--(x0-.5,lower_notch){up}... fi
  {z5-z6}diag_in(6r,5r,1,5l)--cycle;  % diagonals
else:
 y6=-apex_o; z0=whatever[z5r,z6r]=whatever[z7l,z8l];
 p26=lineintersection(1r,2r,5l,6l);
 p47=lineintersection(7r,8r,3l,4l);
 p5=pen_point(6r,5r,tiny,1l);
 p8=pen_point(3r,8l,tiny,7l);
 p0=z0;
fi
p7=pen_pointend(5l,6l,tiny,7r,8r);
p1=serif_path(1,2,a,1/3,M_jut,left);  % upper left serif
p2=dish_serif_path(2,1,b,.45,M_jut,c,.45,cap_jut)(dark); % lower left serif
p3=serif_path(3,4,d,1/3,cap_jut,right); %  upper right serif
p4=dish_serif_path(4,3,e,1/3,cap_jut,f,1/3,cap_jut); % lower right serif
fill p2--p26--p7--p47--p4--p3--p8--p0--p5--p1--cycle;
math_fit(0,max(.5ic#-.5u#,0));
penlabels(0,1,1',2,3,3',4,5,6,7,8); endchar;

cmchar "The letter N";
beginchar("N",10u#,cap_height#,0);
italcorr cap_height#*slant-cap_serif_fit#+cap_jut#-2.5u#+min(.5cap_stem#,u#);
adjust_fit(0,0);
pos1(nib_hair,0); pos2(nib_hair,0); pos3(nib_hair,0); pos4(nib_hair,0);
x1l=x2l=2u; x3r=x4r=w-u;
y1=y3=h+o; y2=y4=-o;
nibstems(6);
x5=x2l-2u; x6l=x2r; y5=y6l=-o;
swoop_in_path5(5,6,a);
tick_path6(6,b,up,.75u);
nibstems(7);
x7r=x3l; x8=x3r+2u; y7r=h+o;
swoop_out_path8(7,8);
tick_path7(7,c,down,-.75u);
nibstems(9,10);
x9=x1r; y9r=y1r; z10l=z4l;
x11=0;
angled_hook_in_path11(11,9,10,d,45);
pos12(stem,90); z12=.5[z2l,z1l];
p12=side_hook_path(12);
fill z4r--z3r var_combined lpath8 & rpath8 & rpath7
 --(lpath7 var_combined lpath8 combined (z3l--lineintersection(3l,4l,10r,9r)))
 --rpath11 
 & (lpath11 combined (z1l--p12--z2l) var_combined rpath5)
 & lpath5 & rpath6
 --(lpath6 combined rpath5 combined (z2r--z1r) combined (lpath11--z4l))
 --cycle;
penlabels(1,2,3,4,5,6,7,8,9,10,11,12); endchar;

cmchar "The letter O";
beginchar("O",14u#-width_adj#,cap_height#,0);
italcorr .7cap_height#*slant-.5u#;
adjust_fit(0,0);
penpos1(thin_cap_band,90);
penpos3(thin_cap_band,-90);
penpos2(cap_curve,180); penpos4(cap_curve,0);
if monospace: x2r=hround 1.5u;
 interim superness:=O_superness;  % make |"O"|, not |"0"|
else: lft x2r=0; fi
x4r=w-x2r; .5[x1,x3]=.5w; x3-x1=6u*slant; y1r=h+o; y3r=-o;
.5[y2,y4]=.5h-vair_corr; y2-y4=.5h*slant; y2l:=y2l+.02h; y4l:=y4l+.02h;
x1l:=x1l+u*slant; x3l:=x3l-u*slant;
make_paths1(rev.l) pulled_super_arc.e(1,2)(.5superpull)
 & pulled_super_arc.e(2,3)(.5superpull)
 & pulled_super_arc.e(3,4)(.5superpull)
 & pulled_super_arc.e(4,1)(.5superpull) & cycle;
fill rpath1;
unfill lpath1;
math_fit(-.3cap_height#*slant-.5u#,ic#-.5u#); penlabels(1,2,3,4); endchar;

cmchar "The letter P";
beginchar("P",12u#,cap_height#,0);
italcorr .75cap_height#*slant-.5u#;
adjust_fit(cap_serif_fit#,0);
pos1(cap_stem',0); pos2(cap_stem,0);
pos0(cap_stem',0); pos0'(cap_stem,0);
x1l=cap_jut; y1=h; y2=0;
x1l=x2l=x0l=x0'l; y0=y7l;y0'=y7r;
penpos3(thin_cap_band,90); penpos4(thin_cap_band,90);
penpos5(cap_curve if hefty:-3stem_corr fi,0);
penpos6(.5[vair,cap_band],-90); penpos7(.5[vair,cap_band],-90);
z3r=z1; y4=y3; y5=(.5+.5slant)[y4l,y6l]; y6=y7;
x7=x2; y7l=vround .515h; x4=.58[x1r,x5l]; x6=.4[x1r,x5l]; x5r=w;
x4l:=x6l:=x4-.25cap_curve;
make_paths1(rev.r) pulled_arc.e(4,5) &
 {{ interim superness:=more_super; pulled_arc.e(5,6) }};  % lobe
p1=serif_path(1,0,a,1/3,cap_jut,left);
p2=dish_serif_path(2,0',c,1/3,slant_adjusted_jut(cap_jut),
 d,1/3,1.1cap_jut); % lower serif
p3=internal_serif_path(1,0,3l,b,1/3,min(cap_jut,x4l-x1r-eps));
fill p2--z0'r--rpath1--p1--cycle;
unfill p3--lpath1--z0r--cycle;
math_fit(0,ic#-2.5u#); penlabels(0,0',1,2,3,4,5,6,7); endchar;

cmchar "The letter Q";
beginchar("Q",14u#-width_adj#,cap_height#,(1.1-.5slant)*comma_depth#);
italcorr .7cap_height#*slant-.5u#;
adjust_fit(0,0);
numeric light_curve; light_curve=cap_curve-hround stem_corr;
penpos1(vair',90); penpos3(vair',-90);
penpos2(light_curve,180); penpos4(light_curve,0);
if monospace: x2r=hround 1.5u;
 interim superness:=O_superness  % make |"Q"| like |"O"|
else: x2r=0; fi
x4r=w-.7hair-u*slant; .5[x1,x3]=.5[x4,x2]; x3-x1=6u*slant; y1r=h+o; y3r=-o;
.5[y2,y4]=.5h-vair_corr; y2-y4=.5h*slant; y2l:=y2l+.02h; y4l:=y4l+.02h;
x1l:=x1l+u*slant; x3l:=x3l-u*slant;
make_paths1(rev.l) pulled_super_arc.e(1,2)(.5superpull)
 & pulled_super_arc.e(2,3)(.5superpull);
make_paths2(rev.l) pulled_super_arc.e(3,4)(.5superpull)
 & pulled_super_arc.e(4,1)(.5superpull);  % bowl
if hefty: penpos5(1.2(.5[cap_bar,light_curve]),0); penpos6(1.2light_curve,0);
 x5=.5w; x6r=hround(w-1.5u);
 y5=vround .28h; y6=-d;
 fill diag_end(6r,5r,.25,1,5l,6l)--diag_end(5l,6l,.5,1,6r,5r)--cycle;  % tail
else:
 pos5(vair,180); pos6(vair,90);
 pos7((1-slant)*cap_curve,85+angle(1,slant)); penpos8(hair,180);
 x6=x3+.1u-slant*(y6-y3); y6r=vround(d+oo+.5vair);
 z5=z3+whatever*(-1,1)=z6+whatever*(1,1);
 y7l=-d; x7l=.58[x6l,x8l]-y7l*slant; y8=-tiny; x8l=w;
 if y7r>y8-thick_vair: y7r:=y8-thick_vair; y7:=.5[y7r,y7l]; fi
 z7'r-z8r=whatever*(z7l-z8l); y7'r=y7r; x7r:=x7'r;
 pos9(.7[hair,stem],0); z9=.5[z6,z7];
 make_paths3(rev.r) z3e{left}...z5e{up}...{right}z6e;
 make_paths4(rev.r) z6e{right}..z9e..z7e{right}
  ...{up}stroke_end.e(up,tiny,8); % tail
 fill rpath1 & var_combine(rpath2, lpath4)
  --combine(rpath4, rpath2) & cycle;
 unfill var_combine(lpath2,rpath4) & combine(rpath3,lpath1) & cycle;
 unfill lpath3 & combine(lpath4,lpath2) & cycle;
fi
math_fit(-.3cap_height#*slant-.5u#,ic#);
penlabels(1,2,3,4,5,6,7,8,9); endchar;

cmchar "The letter R";
beginchar("R",if serifs: 12u#+.5max(2u#,cap_curve#)
 else:12.5u#-.5width_adj# fi,cap_height#,0);
italcorr .75cap_height#*slant- if serifs: 1.75 else: .5 fi\\ u#;
adjust_fit(cap_serif_fit#,0);
pos1(cap_stem',0); pos2(cap_stem',0);
x1l=x2l=cap_jut; y1=h; y2=0;
penpos3(thin_cap_band,90); penpos4(thin_cap_band,90);
penpos5(cap_curve if hefty:-3stem_corr fi,0);
penpos6(.8vair,-90); penpos7(vair,-90);
z3r=z1; y4=y3; y5=(.43+.57slant)[y4l,y6l]; y6l=y7l;
x7=x2r; y7l=vround(.52h+.5vair); x6=.2[x7,x5l];
if serifs: x4=.5w-.5u; x5r=hround(w-.75[hair,stem]-u*slant);
else: x4=.5w+.5u; x5r=hround(w-u); fi
x4l:=x6l:=x4-.125cap_curve;
if x4l-x1r<cap_jut: x4l:=x1r+cap_jut; fi
{{
 interim superness:=hein_super;
make_paths1(rev.r) pulled_arc.e(4,5) & pulled_arc.e(5,6)  % lobe
}};
if serifs: pos6'(.6vair,-90);
 pos8(cap_band,220); pos9(.85cap_curve,-115); pos10(hair,0);
 pos8'(.85cap_curve,200);
 x10r=w; tiny.top y10=.45[y2,y7];
 x9r=.25[x5,x5r];
 y9r=-o;
 if y9l>y10-thick_vair: y9l:=y10-thick_vair; y9:=.5[y9r,y9l]; fi
 y8=1/5[y6',y9];
 y8'=2/3[y6',y9];
 z6'r=z6l + down*vair;
 z8=z6'+whatever*(z9-z6') xscaled 2.5;
 z8'=z6'+whatever*(z9-z6') xscaled 1.05;
 make_paths2(rev.l) z6'e{right}...z8e...z8'e...z9e{right}
  ..{up}stroke_end.e(up,tiny,10);
 p1=serif_path(1,2,a,1/3,cap_jut,left);  % upper serif
 p3=internal_serif_path(1,2,3l,b,1/3,cap_jut);
 p2=dish_serif_path(2,1,c,1/3,slant_adjusted_jut(cap_jut),
  d,1/3,cap_jut);  % lower serif
 fill p2--z7r--rpath2--combine(lpath2,rpath1)--p1--cycle;
 unfill p3--lpath1--z7l--cycle;
else: penpos8(cap_stem-2stem_corr,0); penpos9(cap_stem,0);
 x8=x6+.5u; y8=y6; x9r=hround(w-.5u); y9=0;
 fill z8l--diag_end(8l,9l,.5,1,9r,8r)--z8r--cycle; fi  % tail
math_fit(0,.75ic#); penlabels(0,1,2,3,4,5,6,6',7,8,8',9,10); endchar;

cmchar "The letter S";
beginchar("S",12u#,cap_height#,0);
italcorr cap_height#*slant-u#;
adjust_fit(0,0);
numeric theta; theta=90; slope:=-cosd(theta)/sind(theta);
numeric s_slab; s_slab=if serifs:slab else:vround .2[slab,cap_stem] fi;
numeric s_gap; s_gap=1u;
pos2(stem,nib_angle+180);
pos0(stem,theta); pos7(s_slab,-90);
x0=.5w; x2=x0-u; x7=x0+.5u; y2l=h+o; y2r-y0r=y0l-s_gap-stem-y7l;
y0l=.5h+1u; x3l=2u; x6r=w-u+o;
x3r-x3l=x6r-x6l=2nib_hair;
ellipse_set(2l,3l,4l,0l); ellipse_set(2r,3r,4r,0r); y3=y3r;
ellipse_set(7l,6l,5l,0l); ellipse_set(7r,6r,5r,0r); y6=y6r;
make_paths1(rev.r) super_arc.e(2,3) & z3e{down}
 ...{z5e-z4e}z0e...z6e{down};  % main stroke
pos1(nib_stem,nib_angle-180); pos8(hair,180);
z1=z2+whatever*(nib_draw_dir yscaled .5); x1l=w-1.5u;
pos10(nib_hair,0); pos9(nib_hair,0);
x9r=3u;
y10=y2l; y9=0;
z10r=whatever[z9r,z1r];
same_pos(0',0); z0'r=z0l-s_gap*(dir angle (z0r-z0l));
z3'l=z3'r=z3l-s_gap*dir 45;
pair s_term_dir; s_term_dir=(z10r-z9r) xscaled 1.5;
s_term_dir:=s_term_dir/abs(s_term_dir);
pos7'(2nib_hair,-90+angle s_term_dir);
z7'l=z6l+whatever*s_term_dir+.5u*right; y7'=-o;
pair pd.r[], pd.l[];
pd.r3=down; pd.l3=down rotated -30;
pos7''(2nib_hair,-60); z7''r=z7'l+s_gap*(s_term_dir rotated 90);
x6'r=x6l-s_gap; x6'r-x6'l=x6r-x6l;
slope:=.9; % No idea why this works, but it does
ellipse_set(0'r,6'r,7.1r,7''r);
ellipse_set(0'l,6'l,7.1l,7''l);
make_paths3(rev.r) z3'e{pd.e3}...{z5e-z4e}z0'e...{down}z6'e..{-s_term_dir}z7''e;
x8=.5u;
nibstems(11); z11l=z7'r;
swoop_in_path11(8,11,a);
make_paths2(rev.r) z1e{nib_draw_dir}..{left}z2e;
make_paths10(rev.l) z9e--z10e;
make_paths6(rev.r) z6e{down}..{-s_term_dir}z7'e;
fill lpath10 var_combined lpath2
 & (lpath1 combined lpath10 var_combined rpath3)
 & (lpath3 combined lpath10 var_combined rpath11)
 & lpath11
 %& (p11.2 var_combined lpath10)
 %-- (rpath10 var_combined p11.2)
 & rpath6 & (rpath1 combined rpath10)
 --cycle;

unfill rpath2 combined lpath10 var_combined rpath1 & cycle;

unfill lpath6 combined rpath11 combined rpath3 combined rpath10
 var_combined lpath1 & cycle;
unfill (subpath (2,infinity) of lpath3) combined rpath11 combined rpath10
 var_combined (subpath(0,2) of lpath3) & cycle;
penlabels(0,0',1,1',2,3,3',4,5,6,6',7,7',7'',7.1,8,8',9,10,11); endchar;

cmchar "The letter T";
beginchar("T",12u#,cap_height#,0);
italcorr cap_height#*slant-beak_jut#-.25u#;
adjust_fit(0,0);
y1l=h-stem;
x2l=u-o;
y3l=-o;
x4l=w;
crescent_path1(1,2,3,4,dir 45);
z5=(x2,h+o-stem); z6=(w,h+o);
tilde_path5(5,6,a,2u);
nibstems(7,8);
x7=x8=.5[x5,x6];
y7r=y5-.5u;
y8l=y3r+stem;
p8=small_point_path(8,7,b,.5u);
nibhairs(9,10);
z10r=z7r; nib_align(z9r,z10r); x9r=x7l-.75u;
pos11(nib_hair,0); pos12(nib_hair,0);
x11=x12; x11l=x7r+u; y11r=h+o; y12r=-o;
fill lpath1 & (rpath1 combined (z12r--z11r) var_combined lpath5)
  & rpath5 & (lpath5 combined (z11l--z12l) var_combined rpath1) & cycle;
fill z7r--z9r--z9l--lineintersection(9l,10l,7l,8l)--p8--cycle;
penlabels(1,2,3,4,5,6,7,8,9,10,11,12); endchar;

cmchar "The letter U";
beginchar("U",11u#,cap_height#,0);
italcorr cap_height#*slant-cap_serif_fit#+cap_jut#-2.5u#+min(.5cap_stem#,u#);
adjust_fit(0,0);
nibstems(1,2,3,4);
x1l=x2l=3u; x3r=x4r=w-u;
y1=y3=h+o; y2=y4=-o;
x0=.5u;
nibthinstems(5,7);
x5r=.5[x2r,x4l]+hair; y5l=-o; x6=u;
x7r=x3; y7r=h+o; x8=w;
nibhairs(5',7',9,10'); z5'l=z5l; x9l=w; nib_align(z5,z9l);
z7'r=z7r; z10'r=z10l;
pos11(nib_hair,0);
%x11=.5[x2r,x4l];
x4l-x11r=u;
y11=0;
pos12(nib_hair,0); x12=x11; y12=y3;
forsuffixes e=l,,r: x10e=x11e; nib_align(z10e,z7); endfor
p1=hook_in_path(0,1,b,30);
p4=out_serif_path(4,3,a);
swoop_in_path5(6,5,c);
swoop_out_path7(7,8);
p3421=(z3l--p4) combined (z9r--z5'r) var_combined rpath5
  & lpath5 var_combined (z2r--p1);
fill ((z5l--z9l) var_combined (z3l--p4--z3r) var_combined lpath7 & rpath7) --
  z10r--z12r--
  (z12l--z11l var_combined p3421) &
  ((point infinity of p1)--z2l var_combined (rpath5 & lpath5)) & cycle;
unfill (lineintersection(7'l,10'l,10r,11r)--z7'l) var_combined lpath7 & rpath7
  combined (z3l--lineintersection(5'r,9r,4l,3l))
  -- lineintersection(10r,11r,5'r,9r) -- cycle;
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12); endchar;

cmchar "The letter V";
beginchar("V",11u#,cap_height#,0);
italcorr cap_height#*slant-cap_serif_fit#+cap_jut#-2.5u#+min(.5cap_stem#,u#);
adjust_fit(0,0);
nibstems(1,2,3,4);
x1l=x2l=3u; x3r=x4r=w-u;
y1=y3=h+o; y2=y4=-o;
x0=.5u;
nibthinstems(5,7);
x5r=.5[x2r,x4l]+hair; y5l=-o; x6=u;
y6=stem;
x7r=x3; y7r=h+o; x8=w;
nibhairs(5',7',9,10'); z5'l=z5l; x9l=w; nib_align(z5,z9l);
z7'r=z7r; z10'r=z10l;
pos11(nib_hair,0);
%x11=.5[x2r,x4l];
x4l-x11r=u;
y11=0;
pos12(nib_hair,0); x12=x11; y12=y3;
forsuffixes e=l,,r: x10e=x11e; nib_align(z10e,z7); endfor
p1=hook_in_path(0,1,b,30);
p4=out_serif_path(4,3,a);
swoop_in_path5(6,5,c);
swoop_out_path7(7,8);
p3421=(z3l--p4) combined (z9r--z5'r) var_combined rpath5
  & lpath5 var_combined (z2r--p1);
fill z5l--(lineintersection(5l,9l,4r,3r)--z3r var_combined lpath7) & rpath7
  --z10r--z12r--
  (z12l--z11l var_combined p3421) &
  ((point infinity of p1)--z2l var_combined (rpath5 & lpath5)) & cycle;
unfill (lineintersection(7'l,10'l,10r,11r)--z7'l) var_combined lpath7 & rpath7
  combined (z3l--lineintersection(5'r,9r,4l,3l))
  -- lineintersection(10r,11r,5'r,9r) -- cycle;
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12); endchar;

cmchar "The letter W";
beginchar("W",18u#,cap_height#,0);
italcorr cap_height#*slant+.25u#;
adjust_fit(cap_serif_fit#,cap_serif_fit#);
numeric stem[],outer_jut,upper_notch,alpha,mid_corr;
outer_jut=angled_jut(.8cap_jut,(1/2w,h)); x1l=w-x8r=outer_jut;
stem1=fudged.cap_stem-stem_corr;
stem4=min(cap_hair if hefty:-2stem_corr fi,stem1);
stem2=stem4 if hefty:-2stem_corr fi;
stem3=stem1 if hefty:-2stem_corr fi;
x8-x7=x4-x3; x6-x5=x2-x1; x4-x3=x2-x1-8u*slant;;
x2l=x3l; x6l=x7l;
y1=y8=h; y2=y3=y6=y7=-apex_o; y4=y5=if monospace: vround .6 fi\\ h;
mid_corr=if monospace or hefty:-apex_corr else: 1/3jut fi;
alpha=diag_ratio(4,stem2-stem3+stem4,y1-y2,x8r-x1l+mid_corr-2apex_corr);
penpos1(alpha*stem1,0); penpos2(alpha*stem1,0);
penpos3(alpha*stem2,0); penpos4(alpha*stem2,0);
penpos5(alpha*stem3,0); penpos6(alpha*stem3,0);
penpos7(alpha*stem4,0); penpos8(alpha*stem4,0);
x4l-x1l=floor(x4l-x1l+.5(x5r+mid_corr-x4r)); % |x5r+mid_corr| $\approx$ |x4r|
z23=whatever[z1r,z2r]=whatever[z3l,z4l];
z45=whatever[z3r,z4r]=whatever[z5l,z6l];
z67=whatever[z5r,z6r]=whatever[z7l,z8l]; upper_notch=y4-cap_notch_cut;
p23=notch_cut_path(0,cap_notch_cut,aa,4l,23,1r);
p45=notch_cut_path(upper_notch,cap_notch_cut,aa,3r,45,6l);
p67=notch_cut_path(0,cap_notch_cut,aa,5r,67,8l);
if x4r>x5r: z0=whatever[z5l,z6l]=whatever[z3l,z4l]; fi
if serifs: numeric inner_jut[];
 numeric less_dark; less_dark=.5[acute_darkness,.5];
 if monospace: inner_jut1=inner_jut4=1.5cap_jut;
 elseif hefty: inner_jut1=inner_jut4=cap_jut;
 else:
  (inner_jut1,inner_jut2)=special_serif_inner_juts(.48,.38,1,2,5,6,4);
  (inner_jut3,inner_jut4)=serif_inner_juts(.55,1/3,5,6,8,7);
  p4=dish_serif_path(5,6,e,1/3,inner_jut2,f,1/4,
   inner_jut3); % middle serif
  if known z0: p4:=p4--z0; fi
 fi
 p1=dish_serif_path(1,2,a,1/3,outer_jut,b,1/3,inner_jut1);  % left serif
 p8=dish_serif_path(8,7,c,.35,inner_jut4,d,.45,
  outer_jut)(dark); % right serif
fi
fill p1--pen_pointend(1l,2l,tiny,3r,45)--p45
 --pen_pointend(45,6l,tiny,7r,8r)--p8--p67--p4--p23--cycle;
math_fit(.75u#-cap_height#*slant,ic#-2.5u#);
penlabels(0,1,2,3,4,5,6,7,8,23,45,67); endchar;

cmchar "The letter X";
beginchar("X",13u#,cap_height#,0);
italcorr cap_height#*slant-.25u#;
adjust_fit(cap_serif_fit#,cap_serif_fit#);
numeric stem[],outer_jut[],xjut,alpha[];
stem1=cap_stem-2stem_corr; stem2=min(cap_hair,stem1);
outer_jut0=angled_jut(.9cap_jut,(w,h));
xjut= if serifs: inset_factor*.44u else: 0 fi;
outer_jut3=slant_adjusted_jut(outer_jut0);
outer_jut1=slant_adjusted_jut(outer_jut0+.7xjut)less;
x3l=outer_jut3+2u*slant;
w-x4r=outer_jut;
x2r-x1l+2xjut=x4r-x3l;
.5[x1l,x2r]=.5[x3l,x4r]-2u*slant;
y1=y2=h; y3=y4=0;
alpha1=diag_ratio(1,stem1,h,x4r-x1l);
alpha2=diag_ratio(1,stem2,h,x2r-x3l);
penpos1(alpha1*stem1,0); penpos2(alpha2*stem2,0);
penpos3(alpha2*stem2,0); penpos4(alpha1*stem1,0);
z0=whatever[z1,z4]=whatever[z2,z3];
if hefty:
 x12=x34=x0; y13=y24=y0;
 z12=whatever[z2l,z3l]; z13=whatever[z2l,z3l];
 z24=whatever[z2r,z3r]; z34=whatever[z2r,z3r];
else:
 x_intersection12(r,l);
 x_intersection13(l,l);
 x_intersection24(r,r);
 x_intersection34(l,r);
fi
numeric inner_jut[];
if serifs:
 z0l=z13; z0r=z24;
 (inner_jut1,inner_jut2)=serif_inner_juts(.5,.22, 1,4,2,3);
 (inner_jut3,inner_jut4)=serif_inner_juts(.5,1/4, 3,2,4,1);
else:
 inner_jut1=inner_jut2=inner_jut3=inner_jut4=0;
fi
p1=dish_serif_path(1,0,a,1/3,outer_jut1,
 b,.4,inner_jut1); % upper left serif
p4=dish_serif_path(4,0,c,1/3,inner_jut4,
 d,1/3,outer_jut0);  % lower right serif
p2=dish_serif_path(2,0,e,.48,inner_jut2,
 f,.4,slant_adjusted_jut(outer_jut0));  % upper right serif
p3=dish_serif_path(3,0,g,.35,outer_jut3,
 h,1/2,inner_jut3); % lower left serif
fill z12--p1--z13--p3--z34--p4--z24--p2--cycle;
math_fit(0,.5ic#); penlabels(0,1,2,3,4,12,13,24,34); endchar;

cmchar "The letter Y";
beginchar("Y",13u#,cap_height#,0);
italcorr cap_height#*slant+.45u#;
adjust_fit(cap_serif_fit#,cap_serif_fit#);
numeric left_stem,right_stem,outer_jut[],dy,alpha;
numeric Y_shift; Y_shift=.05cap_stem;
left_stem=cap_stem-stem_corr;
right_stem=min(cap_hair if hefty:-2stem_corr fi,left_stem);
outer_jut1=angled_jut(cap_jut,(w,h));
outer_jut2=slant_adjusted_jut(outer_jut1);
w-x4r=outer_jut2; x1l=outer_jut1-2Y_shift;
x2l=x5l=hround(.5w+Y_shift-.5left_stem+1.5u*slant);
x3r=x5r; y1=y4=h; y2=y3=y5=.42h;
dy=y1-y2; alpha=((x2l-x1l)++dy)/dy;
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
penpos5(left_stem,0); penpos6(left_stem,0); x5=x6; y6=0;
z0=whatever[z1r,z2r]=whatever[z3l,z4l];
p0=notch_cut_path(y2,cap_notch_cut, aa,4l,0,1r);
numeric inner_jut_l,inner_jut_r;
(inner_jut_l,inner_jut_r)=serif_inner_juts(.54,.315, 1,2,4,3);
p1=dish_serif_path(1,2,a,.4,x1l,b,.25,
 inner_jut_l);  % left serif
p4=dish_serif_path(4,3,c,.5,inner_jut_r,d,.45,
 outer_jut2); % right serif
p6=dish_serif_path(6,5,e,1/3,1.2cap_jut+.5Y_shift,f,1/3,
 1.2cap_jut-.5Y_shift); % lower serif
fill p6--z5r--p4--p0--p1--z5l--cycle;
math_fit(.5u#-cap_height#*slant,ic#-4u#);
penlabels(0,1,2,3,4,5,6); endchar;

cmchar "The letter Z";
beginchar("Z",11u#,cap_height#,0);
italcorr cap_height#*slant-.5u#;
adjust_fit(0,0);
numeric arm_thickness[],z_stem;
if hefty: arm_thickness1=Vround(slab-vair_corr); arm_thickness2=slab;
 z_stem=.8[vair,cap_stem];
else: arm_thickness1=slab; arm_thickness2=vround(slab+vair_corr);
 z_stem=.9[vair,cap_stem]; fi
x3l=x4l=w-x1r-2u*slant; x1r=x2r; x3l=0;
y1=h; y2=min(y1,h-2/3arm_thickness1);
y4=0; y3=max(y4,.9arm_thickness2);
numeric alpha; alpha=diag_ratio(1,z_stem-tiny,y2-y3,x2r-x3l);
penpos1(alpha*(z_stem),0); penpos2(alpha*(z_stem),0);
penpos3(alpha*(z_stem),0); penpos4(alpha*(z_stem),0);
pos5(arm_thickness1,90); pos6(.8hair,180);
y5r=h; x5=x1; x6r=x3l+.82u*inset_factor-2u*slant; y6=good.y(y5l-1.03beak)-eps;
pos7(arm_thickness2,-90); pos8(hair,0);
y7r=0; x7=x4; x8r=w-x3l; y8=good.y(y7l+1.1beak)+eps;
p6=arm_path(5,6,a,beak_darkness,-1.5beak_jut);  % upper arm and beak
p8=arm_path(7,8,b,beak_darkness,beak_jut_big);  % lower arm and beak
z2'l=whatever[z2l,z3l]; y2'l=y5l;
z3'r=whatever[z2r,z3r]; y3'r=y7l;
p1=pen_pointend(3'r,2r,tiny,1r,1l);
p4=pen_pointend(2'l,3l,tiny,4l,4r);
fill p6--z2'l--p4--p8--z3'r--p1--cycle;
math_fit(0,.5ic#); penlabels(1,2,3,4,5,6,7,8); endchar;
